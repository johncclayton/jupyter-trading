%import common.WS
%import common.CPP_COMMENT
%import common.WS_INLINE
%import common.NEWLINE
%import common.SIGNED_NUMBER -> NUMBER

INLINE_COMMENT: /\{(?!%)[^}]*\}/

%ignore WS
%ignore WS_INLINE
%ignore CPP_COMMENT
%ignore INLINE_COMMENT

DATA_HEADER: /Data:/
GRAPHS_HEADER: /Graphs:/
IMPORT_HEADER: /Import:/
SETTINGS_HEADER: /Settings:/
BENCHMARK_HEADER: /Benchmark:/
PARAMETERS_HEADER: /Parameters:/
STRATEGY_HEADER: /Strategy:/
TEMPLATE_HEADER: /Template:/
INCLUDE_HEADER: /Include:/
CHARTS_HEADER: /Charts:/
TRADES_HEADER: /Trades:/
NOTES_HEADER: /Notes:/
LIBRARY_HEADER: /Library:/
NAMESPACE_HEADER: /Namespace:/
ORDERSETTINGS_HEADER: /OrderSettings:/
ORDERINCLUDE_HEADER: /OrderInclude:/
RESULTS_HEADER: /Results:/
SCAN_HEADER: /Scan:/
SCANINCLUDE_HEADER: /ScanInclude:/
SCANSETTINGS_HEADER: /ScanSettings:/

RESERVED_IDENTIFIER.10: /(?i)(Symbol|Name|BarNum|InfoID|InfoExpiry|InfoFloat|InfoGICS|InfoMargin|InfoTRBC|FilterNum|InList|ListNum|PointValue|InfoShares|TickSize)/
RESERVED_WORD.9: /(?i)\?[a-zA-Z]+(\?)?/
WATCHLIST_REF: /&[-A-Za-z0-9_.@]+/

start: section+

section: benchmark_section | charts_section | data_section | graphs_section | \
         include_section | library_section | namespace_section | \
         notes_section | import_section | settings_section | parameters_section | \
         strategy_section | template_section | trades_section | order_settings_section | order_include_section | \
         results_section | scan_section | scan_include_section | scan_settings_section

notes_section: NOTES_HEADER NEWLINE? NOTE_BLOCK?
NOTE_BLOCK: /(?ms).+?(?=^[A-Za-z_][A-Za-z0-9_]*:|\Z)/

data_section: DATA_HEADER NEWLINE? declaration*
graphs_section: GRAPHS_HEADER NEWLINE? declaration*
import_section: IMPORT_HEADER NEWLINE? declaration*
include_section: INCLUDE_HEADER (expression NEWLINE | NEWLINE?) declaration*
settings_section: SETTINGS_HEADER NEWLINE? declaration*
benchmark_section: BENCHMARK_HEADER (expression NEWLINE | NEWLINE?) declaration*
parameters_section: PARAMETERS_HEADER NEWLINE? parameters_declaration*
strategy_section: STRATEGY_HEADER (expression NEWLINE | NEWLINE?) declaration*
template_section: TEMPLATE_HEADER (expression NEWLINE | NEWLINE?) declaration*
charts_section: CHARTS_HEADER NEWLINE? declaration*
trades_section: TRADES_HEADER NEWLINE? declaration*
library_section: LIBRARY_HEADER NEWLINE? declaration*
results_section: RESULTS_HEADER NEWLINE? declaration*
scan_section: SCAN_HEADER NEWLINE? declaration*
scan_include_section: SCANINCLUDE_HEADER NEWLINE? declaration*
scan_settings_section: SCANSETTINGS_HEADER NEWLINE? declaration*
order_settings_section: ORDERSETTINGS_HEADER NEWLINE? declaration*
order_include_section: ORDERINCLUDE_HEADER NEWLINE? declaration*

namespace_expr: identifier | /(?i)none/
namespace_section: NAMESPACE_HEADER NEWLINE? namespace_expr NEWLINE?

date_expr: date_literal | "Latest" | "Earliest"
date_identifier: /(?i)StartDate/ | /(?i)EndDate/
include_list_expr: string_literal | field_list | path_name
include_name: / \{ "[^"]*" \} /
aliased_item: (identifier | stock_symbol | symbol_item) ">" identifier
field_item: aliased_item | identifier | stock_symbol | string_literal | symbol_item
declaration: date_declaration | include_list_declaration | normal_declaration
parameters_declaration: parameter_range_declaration | parameter_list_declaration | date_declaration | include_list_declaration | normal_declaration
parameter_range_declaration: identifier ":" _TAB? "from" NUMBER "to" NUMBER "step" NUMBER "def" NUMBER NEWLINE?
parameter_list_declaration: identifier ":" _TAB? NUMBER ("," NUMBER)* NEWLINE?
date_declaration: date_identifier ":" _TAB? date_expr NEWLINE?
include_list_declaration: /(?i)IncludeList:/ _TAB? include_list_expr include_name? NEWLINE?
normal_declaration: identifier ":" _TAB? expression NEWLINE?
expression: logical_expr format_spec? CPP_COMMENT?
format_spec: /\{%[^}]*\}/
logical_expr: comparison_expr (logical_op comparison_expr)*
logical_op: "and" | "or"
comparison_expr: add_expr (comparison_op add_expr)*
add_expr: mul_expr (add_op mul_expr)*
mul_expr: indexed_expr (mul_op indexed_expr)*
mul_op: "*" | "/"
add_op: "+" | "-"
indexed_expr: primary ("[" NUMBER "]")?
primary: "(" expression ")" | base_expr
base_expr: function_call | identifier | NUMBER | boolean_literal | stock_symbol | string_literal | field_list | path_name | RESERVED_IDENTIFIER | WATCHLIST_REF  
date_literal: /\d{8}|\d{4}\/\d{2}\/\d{2}|\d{4}-\d{2}-\d{2}|\d{4}\.\d{2}\.\d{2}|\d{2}-[A-Za-z]{3}-\d{2,4}|\d{1,2}\/\d{1,2}\/\d{2,4}|\d{1,2}-\d{1,2}-\d{2,4}|\d{1,2}\.\d{1,2}\.\d{2,4}/
boolean_literal: /(?i)True/ | /(?i)False/
reserved_identifier: RESERVED_IDENTIFIER
path_name: reserved_word path_literal? | path_literal
reserved_word: RESERVED_WORD
field_list: field_item? ("," field_item? )*
symbol_item: /[%$][\w.]+/
comparison_op: "<" | ">" | "<=" | ">=" | "==" | "<>" | "!=" | "=" 
function_call: (RESERVED_IDENTIFIER | identifier) "(" arguments? ")"
arguments: expression ("," expression)*
identifier: /(?i)(?!Data\b|Graphs\b|Import\b|Settings\b|Benchmark\b|Parameters\b|Strategy\b|Template\b|Include\b|Charts\b|Trades\b|Notes\b|Library\b|Namespace\b|OrderSettings\b|OrderInclude\b|Results\b|Scan\b|ScanInclude\b|ScanSettings\b|True\b|False\b)[a-zA-Z_][a-zA-Z0-9_]*/
string_literal: /"[^"]*"/
stock_symbol: /[$%](?:[$&])?[A-Za-z0-9_.@]+/
path_literal: /[^?\n]+/

_TAB: /\t/
