Notes:	
	use a benchmark copy of each strategy to generate equity curves
	only trade the live strategies when equity is above it's 150-day average
	open Graphs after running this example to see the four equity curves

Include: ?scriptpath?\mr_sample_common.rts // right-click on file name to open

Template: track
	Tracker:	Visible // generate benchmark copy of strategy
	SetupSkip:	if(not IsTracker, Extern(@tracker, S.Equity < Avg(S.Equity, 150)), 0)
	// "IsTracker" will be true in the benchmarks and false in the strategy
	// we need it because BOTH will inherit this SetupSkip formula

Strategy: mr_long // mean-reversion long strategy
	Using:	base, track // <<< uses the above template to add tracking benchmark and referfence its equity
	Side:	Long
	EntrySetup:	Universe and C < (1 - PctExt / 100) * Min(O, C[1], EMA5) // oversold
	EntryLimit:	LongLimit
	ExitLimit:	FillPrice * (1 + Target/100)	// intraday profit target
	ExitRule:	select(C > C[1], "up day", BarsHeld == 5, "time stop") // sell on first up day or after 5 days
	
Strategy: mr_short // mean-reversion short strategy (gets DESTROYED in Jaunary 2021 by the meme stocks)
	Using:	base, track // <<< uses the above template to add tracking benchmark and referfence its equity
	Side:	Short
	EntrySetup:	Universe and C > (1 + PctExt / 100) * Max(O, C[1], EMA5) // overbought
	EntryLimit:	ShortLimit
	ExitLimit:	FillPrice * (1 - Target/100) // intraday profit target
	ExitRule:	select(C < C[1], "down day", BarsHeld == 5, "time stop") // cover on first down day or after 5 days

