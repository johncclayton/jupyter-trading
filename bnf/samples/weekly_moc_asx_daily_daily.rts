Notes:
	version of weekly_moc_asx example converted to a daily barsize strategy with weekly data items

Settings:
	DataFile:	all_asx.rtd // see weekly_moc_asx.rts for Import definition
	StartDate:	Earliest
	EndDate:	Latest
	BarSize:	Daily // script runs on daily interval and Data arrays are daily by default
	
OrderSettings:
	StartDate:	2024-01-01
	EndDate:	Latest
	OrdersMode:	Template
	OrdersTemplate:	Examples\ib_basket_template.csv

Parameters:
	minprice:	1
	minturnover:	1000000 // $1M
	minpctatr:	5 // weekly ATR pct
	setups:	20 // orders to place each Monday (won't all fill)
	sizepos:	10 // fraction of account to use for size
	lookback:	5 // weeks to use for all multi-bar indicators
	stretch:	1 // atrs for limit price
	
Data:
	// explicitly calculate and store weekly bar arrays
	BarSize:	Weekly 
	Liquid:	c >= minprice and ma(c*v, lookback) >= minturnover
	Volatile:	100 * atr(lookback) / c >= minpctatr
	Momentum:	roc(c, lookback)
	LmtPrice:	C - stretch * atr(lookback)
	// switch back to daily for the following
	BarSize:	Daily 
	Setup:	Liquid and Volatile // daily item can reference weekly items
	// performing the above barsize translations in a Data item makes the tests run faster

Strategy: weekly_moc_as_daily_strategy
	BarSize:	Daily
	Side:	Long
	Quantity:	S.Alloc / sizepos / LmtPrice
	QtyType:	Shares
	MaxSetups:	setups
	SetupScore:	Momentum
	EntrySetup:	Setup
	EntryLimit:	LmtPrice // placed as day order at same price on each day of the week
	// use the following for "near close" market order exits
//	ExitRule:	EndOfWeek[-1]
//	ExitTime:	NextClose
	// use the following for closing-auction limit order exits
	ExitLimit:	if (EndOfWeek[-1], Close * 0.8, 0)
	ExitLimitTime:	NextClose
	 // IB ASX commission plus tax
	Commission:	1.1 * Max(6.00, 0.0008 * FillValue)
